/*
TeenyGPSEmulate.h - Class file for the TeenyGPSEmulate Arduino Library.
Copyright (C) *** Need copyright statement here ***

This program is free software: you can redistribute it and/or modify
it under the terms of the version 3 GNU General Public License as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

#include <Arduino.h>
#include "TeenyGPSEmulate.h"

/********************************************************************/
TeenyGPSEmulate::TeenyGPSEmulate(HardwareSerial &serialPort_)
{
  serialPort = &serialPort_;
}

TeenyGPSEmulate::~TeenyGPSEmulate() {
}

/********************************************************************/
bool TeenyGPSEmulate::init(uint32_t baudRate_) {
  baudRate = baudRate_;
  serialPort->begin(baudRate);
  return true;
}

/********************************************************************/
void TeenyGPSEmulate::tick() {
  if(!incomingPacket.validPacket) {
    while(serialPort->available()) {
      processIncomingByte(serialPort->read());
    }
  }
}

/********************************************************************/
void TeenyGPSEmulate::processIncomingByte(uint8_t incomingByte) {

  // synch1
  if(!incomingPacket.receivingPacket) {
    if(incomingByte == incomingPacket.synch1) {
      // This is the start of a binary sentence. Reset flags.
      // Reset the packet byte counter
      incomingPacket.receivingPacket = true;
      incomingPacket.packetCounter = 1;
    } else {
      // This character is unknown or we missed the previous start of a sentence
    }

  // synch2
  } else if(incomingPacket.receivingPacket &&
            (incomingPacket.packetCounter == 1)) {
    if(incomingByte == incomingPacket.synch2) {
      incomingPacket.packetCounter++;
      incomingPacket.rollingChecksumA = 0;
      incomingPacket.rollingChecksumB = 0;
    } else {
      // This character is unknown or we missed the previous start of a sentence
      incomingPacket.receivingPacket = false;
    }

  // messageClass, messageID, payloadLength
  } else if(incomingPacket.receivingPacket &&
            (incomingPacket.packetCounter < 6)) {
    if(incomingPacket.packetCounter == 2) {
      incomingPacket.messageClass = incomingByte;
    } else if(incomingPacket.packetCounter == 3) {
      incomingPacket.messageID = incomingByte;
    } else if(incomingPacket.packetCounter == 4) {
      incomingPacket.payloadLength = incomingByte;
    } else if(incomingPacket.packetCounter == 5) {
      incomingPacket.payloadLength |= (incomingByte << 8);
      incomingPacket.payloadCounter = 0;
    }
    incomingPacket.packetCounter++;
    incomingPacket.rollingChecksumA += incomingByte;
    incomingPacket.rollingChecksumB += incomingPacket.rollingChecksumA;

  // payload
  } else if(incomingPacket.receivingPacket &&
            (incomingPacket.payloadCounter < incomingPacket.payloadLength)) {
    if(incomingPacket.payloadCounter < sizeof(incomingPacket.payload)) {
      // Not storing large full packets, just checking and responding with NAK
      incomingPacket.payload[incomingPacket.payloadCounter] = incomingByte;
    }
    incomingPacket.payloadCounter++;
    incomingPacket.packetCounter++;
    incomingPacket.rollingChecksumA += incomingByte;
    incomingPacket.rollingChecksumB += incomingPacket.rollingChecksumA;

  // checksumA
  } else if(incomingPacket.receivingPacket &&
            (incomingPacket.packetCounter == (incomingPacket.payloadLength + 6))) {
    incomingPacket.checksumA = incomingByte;
    incomingPacket.packetCounter++;

  // checksumB
  } else if(incomingPacket.receivingPacket &&
            (incomingPacket.packetCounter == (incomingPacket.payloadLength + 7))) {
    incomingPacket.checksumB = incomingByte;
    incomingPacket.packetCounter++;
    if((incomingPacket.checksumA == incomingPacket.rollingChecksumA) &&
       (incomingPacket.checksumB == incomingPacket.rollingChecksumB)) {
      incomingPacket.validPacket = true;
    }
    incomingPacket.receivingPacket = false;
  }
}

/********************************************************************/
// forward declarations
bool msg_update(const char* msgStr);

/********************************************************************/
void TeenyGPSEmulate::processIncomingPacket() {
  if(incomingPacket.validPacket) {
    if(receivedPacket.validPacket) {
      msg_update("ERROR: LOST CMD PKT");
    } else {
      receivedPacket = incomingPacket;
    }
    incomingPacket.validPacket = false;
  }
  if(receivedPacket.validPacket) {

    // Poll CFG-PRT COM_PORT_UART1
    if((receivedPacket.messageClass == TGPSE_UBX_CLASS_CFG) &&
       (receivedPacket.messageID == TGPSE_UBX_CFG_PRT) &&
       (receivedPacket.payloadLength == 1) &&
       (receivedPacket.payload[0] == TGPSE_COM_PORT_UART1)) {
      // Return configuration for COM_PORT_UART1
      responsePacket.messageClass = receivedPacket.messageClass;
      responsePacket.messageID = receivedPacket.messageID;
      responsePacket.payloadLength = sizeof(TGPSE_UBX_CFG_PRT_PAYLOAD);
      memcpy(responsePacket.payload, TGPSE_UBX_CFG_PRT_PAYLOAD, sizeof(TGPSE_UBX_CFG_PRT_PAYLOAD));
      responsePacket.payload[0] = TGPSE_COM_PORT_UART1;
      responsePacket.payload[8] = baudRate;
      responsePacket.payload[9] = baudRate >> 8;
      responsePacket.payload[10] = baudRate >> 16;
      responsePacket.payload[11] = baudRate >> 24;
      responsePacket.payload[14] = emulatorSettings.outputUBX ? 0x01 : 0x00;
      calcChecksum(&responsePacket);
      responsePacket.validPacket = true;
      buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, true);

    // Set CFG-PRT COM_PORT_UART1
    } else if((receivedPacket.messageClass == TGPSE_UBX_CLASS_CFG) &&
       (receivedPacket.messageID == TGPSE_UBX_CFG_PRT) &&
       (receivedPacket.payloadLength == sizeof(TGPSE_UBX_CFG_PRT_PAYLOAD)) &&
       (receivedPacket.payload[0] == TGPSE_COM_PORT_UART1)) {
      // Update configuration for COM_PORT_UART1 and ACK if not changing baudRate
      requestedBaudRate = receivedPacket.payload[8];
      requestedBaudRate |= receivedPacket.payload[9] << 8;
      requestedBaudRate |= receivedPacket.payload[10] << 16;
      requestedBaudRate |= receivedPacket.payload[11] << 24;
      if(requestedBaudRate != baudRate) {
        init(requestedBaudRate);
        receivedPacket.validPacket = false;
        return;
      } else if(((receivedPacket.payload[14] & 0xFE) != 0x00) ||
                (receivedPacket.payload[15] != 0x00)) {
        buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, false);
      } else {
        emulatorSettings.outputUBX = receivedPacket.payload[14] & 0x01;
        buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, true);
      }

    // Poll CFG-RATE (measureRate and navigationRate)
    } else if((receivedPacket.messageClass == TGPSE_UBX_CLASS_CFG) &&
       (receivedPacket.messageID == TGPSE_UBX_CFG_RATE) &&
       (receivedPacket.payloadLength == 0)) {
      // Return measureRate and navigationRate
      responsePacket.messageClass = receivedPacket.messageClass;
      responsePacket.messageID = receivedPacket.messageID;
      responsePacket.payloadLength = sizeof(TGPSE_UBX_CFG_RATE_PAYLOAD);
      memcpy(responsePacket.payload, TGPSE_UBX_CFG_RATE_PAYLOAD, sizeof(TGPSE_UBX_CFG_RATE_PAYLOAD));
      responsePacket.payload[0] = emulatorSettings.measureRate;
      responsePacket.payload[1] = emulatorSettings.measureRate >> 8;
      responsePacket.payload[2] = emulatorSettings.navigationRate;
      responsePacket.payload[3] = emulatorSettings.navigationRate >> 8;
      calcChecksum(&responsePacket);
      responsePacket.validPacket = true;
      buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, true);

    // Set CFG-RATE (measureRate and navigationRate)
    } else if((receivedPacket.messageClass == TGPSE_UBX_CLASS_CFG) &&
       (receivedPacket.messageID == TGPSE_UBX_CFG_RATE) &&
       (receivedPacket.payloadLength == sizeof(TGPSE_UBX_CFG_RATE_PAYLOAD))) {
      // Update measureRate and navigationRate
      emulatorSettings.measureRate = receivedPacket.payload[0];
      emulatorSettings.measureRate |= receivedPacket.payload[1] << 8;
      emulatorSettings.navigationRate = receivedPacket.payload[2] << 16;
      emulatorSettings.navigationRate |= receivedPacket.payload[3] << 8;
      buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, true);

    // Poll CFG-MSG (autoPVT enable/disable)
    } else if((receivedPacket.messageClass == TGPSE_UBX_CLASS_CFG) &&
       (receivedPacket.messageID == TGPSE_UBX_CFG_MSG) &&
       (receivedPacket.payloadLength == 2) &&
       (receivedPacket.payload[0] == TGPSE_UBX_CLASS_NAV) &&
       (receivedPacket.payload[1] == TGPSE_UBX_NAV_PVT)) {
      // Return autoPVT setting
      responsePacket.messageClass = receivedPacket.messageClass;
      responsePacket.messageID = receivedPacket.messageID;
      responsePacket.payloadLength = 3;
      responsePacket.payload[0] = TGPSE_UBX_CLASS_NAV;
      responsePacket.payload[1] = TGPSE_UBX_NAV_PVT;
      responsePacket.payload[2] = emulatorSettings.autoPVT;
      calcChecksum(&responsePacket);
      responsePacket.validPacket = true;
      buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, true);

    // Set CFG-MSG (autoPVT enable/disable)
    } else if((receivedPacket.messageClass == TGPSE_UBX_CLASS_CFG) &&
       (receivedPacket.messageID == TGPSE_UBX_CFG_MSG) &&
       (receivedPacket.payloadLength == 3) &&
       (receivedPacket.payload[0] == TGPSE_UBX_CLASS_NAV) &&
       (receivedPacket.payload[1] == TGPSE_UBX_NAV_PVT)) {
      // Update autoPVT setting
      emulatorSettings.autoPVT = receivedPacket.payload[2];
      buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, true);

    // Poll MON-VER (receiver and software versions)
    } else if((receivedPacket.messageClass == TGPSE_UBX_CLASS_MON) &&
       (receivedPacket.messageID == TGPSE_UBX_MON_VER) &&
       (receivedPacket.payloadLength == 0)) {
      // Return receiver and software versions
      responsePacket.messageClass = receivedPacket.messageClass;
      responsePacket.messageID = receivedPacket.messageID;
      responsePacket.payloadLength = sizeof(TGPSE_UBX_MON_VER_PAYLOAD);
      memcpy(responsePacket.payload, TGPSE_UBX_MON_VER_PAYLOAD, sizeof(TGPSE_UBX_MON_VER_PAYLOAD));
      calcChecksum(&responsePacket);
      responsePacket.validPacket = true;
      // *** DON'T ACK UBX-MON-VER REQUESTS ***

    // NAK anything else (because it is not supported
    } else {
      unknownPacket = receivedPacket;
      buildAcknowledegePacket(receivedPacket.messageClass, receivedPacket.messageID, false);
    }

    receivedPacket.validPacket = false;
    sendPackets();
  }
}
/*
poll UBX-CFG-PRT  for begin() and setOutputUBX - Return UBX-CFG-PRT and ACK
set  UBX-CFG-PRT  for setOutputUBX - Returns ACK if not changing baudRate
poll UBX-CFG-RATE for setMeasRate and setNavRate - Returns UBX-CFG-RATE and ACK
set  UBX-CFG-RATE for setMeasRate and setNavRate - Returns ACK
set  UBX-CFG-MSG  for setAutoPVT - Returns ACK
set  UBX-CFG-CFG  for saveConfig - Returns ACK
poll UBX-MON-VER  for getProtocolVersion - Returns UBX-MON-VER only
*/

/********************************************************************/
void TeenyGPSEmulate::buildAcknowledegePacket(uint8_t messageClass, uint8_t messageID, bool ack) {
  acknowledgePacket.messageClass = TGPSE_UBX_CLASS_ACK;
  acknowledgePacket.messageID = ack ? TGPSE_UBX_ACK_ACK : TGPSE_UBX_ACK_NAK;
  acknowledgePacket.payloadLength = 2;
  acknowledgePacket.payload[0] = messageClass;
  acknowledgePacket.payload[1] = messageID;
  calcChecksum(&acknowledgePacket);
  acknowledgePacket.validPacket = true;
}

/********************************************************************/
void TeenyGPSEmulate::sendPackets() {
  if(responsePacket.validPacket) {
    sendPacket(&responsePacket);
    responsePacket.validPacket = false;
  }
  if(acknowledgePacket.validPacket) {
    sendPacket(&acknowledgePacket);
    acknowledgePacket.validPacket = false;
  }
}

/********************************************************************/
void TeenyGPSEmulate::sendPacket(ubxPacket_t *pkt) {
  serialPort->write(pkt->synch1);
  serialPort->write(pkt->synch2);
  serialPort->write(pkt->messageClass);
  serialPort->write(pkt->messageID);
  serialPort->write(pkt->payloadLength & 0xFF);
  serialPort->write(pkt->payloadLength >> 8);
  for(uint16_t i = 0; i < pkt->payloadLength; i++) {
    serialPort->write(pkt->payload[i]);
  }
  serialPort->write(pkt->checksumA);
  serialPort->write(pkt->checksumB);
}

/********************************************************************/
// Given a message, calc and store the two byte "8-Bit Fletcher" checksum over the entirety of the message
// This is called before we send a command message
void TeenyGPSEmulate::calcChecksum(ubxPacket_t *pkt) {
  pkt->checksumA = 0;
  pkt->checksumB = 0;

  pkt->checksumA += pkt->messageClass;
  pkt->checksumB += pkt->checksumA;

  pkt->checksumA += pkt->messageID;
  pkt->checksumB += pkt->checksumA;

  pkt->checksumA += (pkt->payloadLength & 0xFF);
  pkt->checksumB += pkt->checksumA;

  pkt->checksumA += (pkt->payloadLength >> 8);
  pkt->checksumB += pkt->checksumA;

  for(uint16_t i = 0; i < pkt->payloadLength; i++) {
    pkt->checksumA += pkt->payload[i];
    pkt->checksumB += pkt->checksumA;
  }
}

